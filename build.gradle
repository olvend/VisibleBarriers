buildscript {
    repositories {
        maven { url 'http://files.minecraftforge.net/maven' }
        maven { url 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1.3'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

minecraft {
    version = "${project.loader_version}-${project.minecraft_version}"
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = false
}

mixin {
    add sourceSets.main, "${project.mod_id}.refmap.json"
}

repositories {
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    compile project(':compat_1_9')

    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
}

processResources {
    def propertyMap = (file('src/main/resources/mcmod.info').text =~ /\$\{([a-zA-Z0-9_]+)}/)
            .collect { it[1] }
            .unique()
            .collectEntries { [(it): project.hasProperty(it) ? project[it] : it] }

    inputs.properties propertyMap

    filesMatching('mcmod.info') {
        expand propertyMap
    }
}



jar {
    dependsOn ':compat_1_9:build'

    manifest.attributes(
            'TweakOrder': 0,
            'ModSide': 'CLIENT',
            'MixinConfigs': "${project.mod_id}.mixins.json",
            'FMLCorePluginContainsFMLMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'ForceLoadAsMod': true
    )

    def includedJars = [
            'compat_1_9.jar',
            'mixin-0.7.11-SNAPSHOT.jar'
    ]

    from(configurations.runtimeClasspath.filter { file ->
        includedJars.any { (it == file.name) }
    }.collect { zipTree(it) }) {
        exclude 'META-INF/*', 'dummyThing', 'LICENSE.txt'
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}
